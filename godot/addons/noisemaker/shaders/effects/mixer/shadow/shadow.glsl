#version 450
// mixer/shadow program shadow — ported from glsl/shadow.glsl. No-layout effect: params and engine globals are injected as #defines;
// bool params arrive as floats, int params via int().
layout(set = 0, binding = 1) uniform sampler2D inputTex;
layout(set = 0, binding = 2) uniform sampler2D tex;
layout(location = 0) in vec2 v_uv;
layout(location = 0) out vec4 fragColor;

/*
 * Shadow / Glow mixer shader
 *
 * Uses one input as a mask to cast an offset, blurred shadow or glow
 * onto the other input. The mask channel is thresholded, then the
 * resulting silhouette is offset, blurred, and spread to form the shadow.
 */
// Extract a single channel from a color
float getChannel(vec4 color_, int channel) {
    if (channel == 0) return color_.r;
    if (channel == 1) return color_.g;
    if (channel == 2) return color_.b;
    return color_.a;
}

void main() {
    vec2 globalCoord = gl_FragCoord.xy + tileOffset;
    vec2 uv = globalCoord / fullResolution;

    // Base image is the non-mask source
    vec4 baseColor = (int(maskSource) == 0) ? texture(tex, gl_FragCoord.xy / vec2(textureSize(tex, 0))) : texture(inputTex, gl_FragCoord.xy / vec2(textureSize(inputTex, 0)));

    // Mask UV shifted by shadow offset, scaled for print resolution
    vec2 maskUV = uv - vec2(offsetX, offsetY) * 0.1 * renderScale;

    // Gaussian blur of thresholded mask
    float shadowMask = 0.0;
    float totalWeight = 0.0;

    // Scale blur by renderScale and cap at overlap
    float blurPixels = min(blur * renderScale, 256.0);
    float sigma = max(blurPixels, 0.001);
    float sigma2 = 2.0 * sigma * sigma;

    for (int x = -5; x <= 5; x++) {
        for (int y = -5; y <= 5; y++) {
            vec2 offset = vec2(float(x), float(y)) * blurPixels / resolution;
            vec2 sampleUV = maskUV + offset;

            // Convert global UV to local UV for tile-local texture sampling
            vec2 localUV = (sampleUV * fullResolution - tileOffset) / vec2(textureSize(inputTex, 0));

            // Apply wrap mode to sample UVs
            float thresholded = 0.0;
            if (int(wrap) == 0) {
                // hide: treat out-of-bounds as empty
                if (localUV.x >= 0.0 && localUV.x <= 1.0 && localUV.y >= 0.0 && localUV.y <= 1.0) {
                    vec4 maskSample = (int(maskSource) == 0)
                        ? texture(inputTex, localUV)
                        : texture(tex, localUV);
                    thresholded = step(threshold, getChannel(maskSample, int(sourceChannel)));
                }
            } else {
                vec2 wrappedUV = localUV;
                if (int(wrap) == 1) {
                    // mirror
                    wrappedUV = abs(mod(localUV + 1.0, 2.0) - 1.0);
                } else if (int(wrap) == 2) {
                    // repeat
                    wrappedUV = fract(localUV);
                } else {
                    // clamp
                    wrappedUV = clamp(localUV, 0.0, 1.0);
                }
                vec4 maskSample = (int(maskSource) == 0)
                    ? texture(inputTex, wrappedUV)
                    : texture(tex, wrappedUV);
                thresholded = step(threshold, getChannel(maskSample, int(sourceChannel)));
            }

            float dist2 = float(x * x + y * y);
            float weight = exp(-dist2 / sigma2);

            shadowMask += thresholded * weight;
            totalWeight += weight;
        }
    }
    shadowMask /= totalWeight;

    // Spread amplifies the mask to expand the shadow
    shadowMask = clamp(shadowMask * (1.0 + spread), 0.0, 1.0);

    // Composite shadow onto base
    vec3 withShadow = mix(baseColor.rgb, color, shadowMask);

    // Composite mask source (foreground) on top of the shadow
    vec4 fgSample = (int(maskSource) == 0)
        ? texture(inputTex, gl_FragCoord.xy / vec2(textureSize(inputTex, 0)))
        : texture(tex, gl_FragCoord.xy / vec2(textureSize(tex, 0)));
    float fgMask = step(threshold, getChannel(fgSample, int(sourceChannel)));
    vec3 result = mix(withShadow, fgSample.rgb, fgMask);

    fragColor = vec4(result, baseColor.a);
}
