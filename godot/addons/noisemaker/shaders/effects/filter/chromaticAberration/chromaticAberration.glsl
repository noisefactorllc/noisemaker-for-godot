#version 450
// filter/chromaticAberration program chromaticAberration — ported from glsl/chromaticAberration.glsl. No-layout effect: params
// and engine globals are injected as #defines; bool params arrive as floats, int params via int().
layout(set = 0, binding = 1) uniform sampler2D inputTex;
layout(location = 0) in vec2 v_uv;
layout(location = 0) out vec4 fragColor;

/*
 * Chromatic aberration effect.
 */
#define PI 3.14159265359
#define ar fullResolution.x / fullResolution.y

float map(float value, float inMin, float inMax, float outMin, float outMax) {
    return outMin + (outMax - outMin) * (value - inMin) / (inMax - inMin);
}

void main() {
    vec2 globalCoord = gl_FragCoord.xy + tileOffset;
    vec2 uv = globalCoord / fullResolution;
    vec2 fullRes = fullResolution.x > 0.0 ? fullResolution : resolution;
    vec2 globalUV = (gl_FragCoord.xy + tileOffset) / fullRes;
    float globalAspect = fullRes.x / fullRes.y;

    vec2 diff = vec2(0.5 * globalAspect, 0.5) - vec2(globalUV.x * globalAspect, globalUV.y);
    float centerDist = length(diff);

    float aberrationOffset = map(aberrationAmt, 0.0, 100.0, 0.0, 0.05) * centerDist * PI * 0.5;

    float redOffset = mix(clamp(uv.x + aberrationOffset, 0.0, 1.0), uv.x, uv.x);
    vec4 red = texture(inputTex, ((vec2(redOffset, uv.y)) * fullResolution - tileOffset) / vec2(textureSize(inputTex, 0)));

    vec4 green = texture(inputTex, gl_FragCoord.xy / vec2(textureSize(inputTex, 0)));

    float blueOffset = mix(uv.x, clamp(uv.x - aberrationOffset, 0.0, 1.0), uv.x);
    vec4 blue = texture(inputTex, ((vec2(blueOffset, uv.y)) * fullResolution - tileOffset) / vec2(textureSize(inputTex, 0)));

    // chromatic aberration - extract color fringing edges only
    vec3 aberrated = vec3(red.r, green.g, blue.b);
    vec3 edges = aberrated - green.rgb;

    // scale original by passthru and add to edges
    vec3 original = green.rgb * map(passthru, 0.0, 100.0, 0.0, 2.0);

    fragColor = vec4(min(edges + original, 1.0), green.a);
}
